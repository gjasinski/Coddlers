buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
    }
}

plugins {
    id 'org.liquibase.gradle' version '2.0.1'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.moowork.node"

group = 'pl'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.apache.logging.log4j:log4j-core:2.11.0')
    compile('org.postgresql:postgresql:9.4-1206-jdbc42')
    compile('io.springfox:springfox-swagger2:2.9.0')
    compile('io.springfox:springfox-swagger-ui:2.9.0')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('io.jsonwebtoken:jjwt:0.9.0')
    compile('javax.mail:javax.mail-api:1.6.1')
    compile('com.sun.mail:javax.mail:1.6.1')
    compile('javax.activation:activation:1.1.1')
    compile('org.liquibase:liquibase-core:3.5.3')
    compile('com.mattbertolini:liquibase-slf4j:2.0.0')
    liquibaseRuntime('org.liquibase:liquibase-core')
    liquibaseRuntime('org.liquibase.ext:liquibase-hibernate5:3.6')
    liquibaseRuntime(sourceSets.main.compileClasspath)
    runtime('com.h2database:h2')
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.mockito:mockito-all:2.0.2-beta')
    testCompile 'org.spockframework.spock:spock-core:spock-1.1'
    testCompile 'org.spockframework.spock:spock-spring:spock-1.1'
    testCompile 'com.jayway.awaitility:awaitility:1.7.0'
}

def webappDir = "$projectDir/src/main/webapp"
sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
        }
    }
}

node {
    version = '10.0.0'
    npmVersion = '5.6.0'
    download = true
    workDir = file("${project.buildDir}/node")
    nodeModulesDir = file("$webappDir")
}

task buildAngular(type: NpmTask) {
    args = ['run', 'build']
}
buildAngular.dependsOn(npm_install)

processResources {
    dependsOn "buildAngular"
}

if (!project.hasProperty('runList')) {
    project.ext.runList = 'main'
}

project.ext.diffChangelogFile = 'src/main/resources/db/liquibase/changelog/' + new Date().format('yyyyMMddHHmmss') + '_changelog.xml'

liquibase {
    activities {
        main {
            driver 'org.postgresql.Driver'
            url 'jdbc:postgresql://localhost:5432/coddlers'
            username 'postgres'
            password '6lettersAGHUST'
            changeLogFile 'src/main/resources/db/liquibase/master.xml'
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
        diffLog {
            driver 'org.postgresql.Driver'
            url 'jdbc:postgresql://localhost:5432/coddlers'
            username 'postgres'
            password '6lettersAGHUST'
            changeLogFile project.ext.diffChangelogFile
            referenceUrl 'hibernate:spring:com.mycompany.myapp.domain?dialect=org.hibernate.dialect.H2Dialect&amp;hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&amp;hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
            defaultSchemaName ''
            logLevel 'debug'
            classpath 'src/main/resources/'
        }
    }

    runList = project.ext.runList
}

task liquibaseDiffChangelog(dependsOn: compileJava, type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    main = "liquibase.integration.commandline.Main"

    args "--driver=" + 'org.postgresql.Driver'
    args "--url=" +'jdbc:postgresql://localhost:5432/coddlers'
    args "--username=" + 'postgres'
    args "--password=" + '6lettersAGHUST'
    args "--changeLogFile=" + project.ext.diffChangelogFile
    args "--referenceUrl=" + 'hibernate:spring:com.example?dialect=org.hibernate.dialect.PostgreSQLDialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy'
    args "--logLevel=" + 'debug'
    args "diffChangeLog"
}
